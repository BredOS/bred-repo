# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - swap zstd for gzip in depends, remove gzip optdepend
#  - patch to revert back to gzip for default compression
#  - patch for gzip kernel image support (https://github.com/archlinux/mkinitcpio/pull/86)

pkgname=mkinitcpio
pkgver=37
pkgrel=2
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://gitlab.archlinux.org/archlinux/mkinitcpio/mkinitcpio'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'binutils' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'systemd')
makedepends=('asciidoc')
checkdepends=('bash-bats' 'bash-bats-assert' 'lzop')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-use-gzip-for-compression-by-default.patch'
        '0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch'
        '0003-check-for-ALARM-kernels-in-run_post_hooks.patch'
        '0004-fix-for-ALARM.patch')
install=mkinitcpio.install
sha512sums=('45548350cd66de6ba2ba5260db3c72d0b6153be36c068ebbd81725252d1dc62fd5dd798acd4a0245c7b58710fa2fea7c3066bd99ecd498dfc154177459af2038'
            'SKIP'
            '24f8431b23ee039fc1aee7b10482d310a8fe37a36975e83aa31d3d0d35d12973c1397057876425fe712be25e999b10753ae05613f3900eccfe5af47e86eda202'
            'e7c14829c0b5c1330819bfef00923f92a1eb59f6d9cf41b8ac421dc970497b9c75e093b37a41f63aa75f9c11ba3ac72c5385d3a00c6a7686cfda82a1a9ad1883'
            'd16c37550da527ef1ca1451c9c81c52197f10a30dfa91aa434a55562f06d4ac97c90f1cb3008b1ba612d6ccf3a46683e001a6e2ba04088d2169274e561183f24'
            '89bb9b6f7d72f6e8877c88402e3e5abef06cf28362a3019c0ba1a8ec360445a15243aee127724db3c007824728a7595cb7c07eff6c45edd0a2d23661c8424120')
b2sums=('84c5122c3775f136bb768580519cb91a18769184d947cfe0e2c714607fe3e7f3e210cf4b58252fad831be8b211e18d7bef33acede97fa273775437d11f25fe07'
        'SKIP'
        'f62db2646bb8828de66e73ea479b2276bba970c5338451e2df4122012d42f453e6fc6892702a58cf36b94f759a8ed7d2f65782067922578d81ae1aaa8cc78806'
        'e0dc41210fb7b811624758022b4254c396d243460e63c86358fe46dc18510a39e021ff9c53944dffb8ec5fbd37681c47f4f83714b63751b7a57d7d9710f47cca'
        '107ef9ee2623a3634a31d62b49efc316615e4c34c847f27a55886c93b2bdb87020bcd7f513ab922de730fe4a6d3bffd26a82180467c14c0993c603bc8801b5df'
        '21d297e90032fdcb42cc91d4e4713e6ef3aed4ef4b519ed3cc392724dd8c2f12060935853909b77e7bb4e56e7ff3c6c551849fda8ad8972f1f2f8b7a62ab0a1f')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              'C100346676634E80C940FB9E9C02FF419FECBE16')   # Morten Linderud

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-use-gzip-for-compression-by-default.patch
  patch -p1 -i ../0002-functions-handle-gzip-compressed-kernels-in-kver_gen.patch
  patch -p1 -i ../0003-check-for-ALARM-kernels-in-run_post_hooks.patch
  patch -p1 -i ../0004-fix-for-ALARM.patch
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
